
Функция Index() Экспорт

	ОбщееСостояниеАвторизации = Новый Структура("ВсеПользователи, Куки", ПользователиИнформационнойБазы, ЗапросHttp.Cookies);

	Возврат Представление(ОбщееСостояниеАвторизации);
КонецФункции

Функция Create() Экспорт

	Если ЗапросHttp.Метод = "POST" Тогда
		СоздатьПользователя();
		Возврат Перенаправление(АдресДействия("Index"));
	Иначе
		Возврат Представление("UserForm");
	КонецЕсли;

КонецФункции

Функция Edit() Экспорт

	Идентификатор = ЗначенияМаршрута["id"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление(АдресДействия("Index"))
	КонецЕсли;

	Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Идентификатор);
	Если Пользователь = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Если ЗапросHttp.Метод = "POST" Тогда
		ОбновитьПользователя(Пользователь);
		Возврат Перенаправление(АдресДействия("Index"));
	Иначе
		Возврат Представление("UserForm", Пользователь);
	КонецЕсли;

КонецФункции

Функция Login() Экспорт
	Если ЗапросHttp.Метод = "POST" Тогда
		Пользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ЗапросHttp.ДанныеФормы["Name"]);
		Если Пользователь = Неопределено Тогда
			СостояниеМодели.ДобавитьОшибку("Name","Неверное имя пользователя или пароль");
		Иначе
			Если Не ПользователиИнформационнойБазы.АвторизоватьПоПаролю(Пользователь, ЗапросHttp.ДанныеФормы["Password"]) Тогда
				СостояниеМодели.ДобавитьОшибку("All","Неверное имя пользователя или пароль");
			КонецЕсли;
		КонецЕсли;

		Если СостояниеМодели.Корректно Тогда
			Возврат Перенаправление(АдресДействия("Index"));
		Иначе
			Возврат Представление("LoginForm");
		КонецЕсли;
	Иначе
		Возврат Представление("LoginForm");
	КонецЕсли;
КонецФункции

&Авторизовать
Функция Secret() Экспорт

	Возврат Представление(ПользователиИнформационнойБазы);

КонецФункции

&Авторизовать
Функция Logout() Экспорт

	ПользователиИнформационнойБазы.СброситьАутентификацию();

	Возврат Перенаправление(АдресДействия("Index"));

КонецФункции


Процедура СоздатьПользователя()

	Пользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
	ОбновитьПользователя(Пользователь);

КонецПроцедуры

Процедура ОбновитьПользователя(Пользователь)
	Пользователь.Имя    = ЗапросHttp.ДанныеФормы["Name"];
	Пользователь.Пароль = ЗапросHttp.ДанныеФормы["Password"];
	Попытка
		Пользователь.Записать();
	Исключение
		ПараметрыОшибки = ИнформацияОбОшибке().Параметры;
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		Если ТипЗнч(ПараметрыОшибки) = Тип("Массив") Тогда
			Для Каждого Ошибка Из ПараметрыОшибки Цикл
				ТекстОшибки = ТекстОшибки + Символы.ПС + Ошибка;
			КонецЦикла;
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры
